# SPDX-FileCopyrightText: 2025 Jonas Fierlings <fnoegip@gmail.com>
#
# SPDX-License-Identifier: 0BSD
#
# Requires [just](https://just.systems).

set unstable
set windows-shell := ["pwsh", "-C"]

CALL := if os_family() == "windows"  { "& " } else { "" }

# Recipe that is executed when no recipe is given on the commandline.
[private]
list:
    @{{ CALL }}{{ quote(just_executable()) }} --justfile {{ quote(source_file()) }} --list --list-submodules

import "../../scripts/maintenance.just"

[group('install')]
[windows]
[working-directory("../..")]
[doc('Bootstrap the europa configuration.')]
bootstrap *ARGS:
    & hosts/europa/Windows-Bootstrap.ps1 {{ ARGS }}

[group('deploy')]
[windows]
[working-directory("../..")]
[doc('Update the windows dotfiles.')]
update *ARGS:
    dotter -v {{ ARGS }}
