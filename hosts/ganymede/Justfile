# Requires [just](https://just.systems).

set unstable
set windows-shell := ["pwsh", "-C"]

CALL := if os_family() == "windows"  { "& " } else { "" }
HOST := "ganymede"
PROVISIONING_DIR := ".provisioning"
ROOT := ".." / ".."
SSH_HOSTKEY := "etc" / "ssh" / "ssh_host_ed25519_key"

# Recipe that is executed when no recipe is given on the commandline.
[private]
list:
    @{{ CALL }}{{ quote(just_executable()) }} --justfile {{ quote(source_file()) }} --list

import "../../scripts/maintenance.just"
import "../../scripts/provisioning.just"

[group('install')]
[script]
[unix]
[working-directory("../..")]
[doc('Prepare a fresh installation of the ganymede configuration by generating SSH keys and passwords.')]
prepare-install:
    just --justfile {{ quote(source_file()) }} generate-ssh-key {{ quote(HOST + " host key") }} {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST / "persist" / SSH_HOSTKEY)) }}
    just --justfile {{ quote(source_file()) }} generate-ssh-key {{ quote(HOST + " initrd host key") }} {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST / "persist" / "boot" / SSH_HOSTKEY)) }}
    just --justfile {{ quote(source_file()) }} update-sops-key {{ quote(HOST) }} {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST / "persist" / SSH_HOSTKEY + ".pub")) }} {{ quote(absolute_path(ROOT / ".sops.yaml")) }} {{ quote(absolute_path(ROOT / "hosts" / HOST / "secrets.yaml")) }}
    just --justfile {{ quote(source_file()) }} generate-disk-password {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST + ".key")) }}

[group('install')]
[unix]
[working-directory("../..")]
[doc('Install the ganymede configuration using nixos-anywhere.')]
install *ARGS: prepare-install
    nix run github:nix-community/nixos-anywhere -- \
        --print-build-logs \
        --flake {{ quote(".#" + HOST) }} \
        --extra-files {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST)) }} \
        --disk-encryption-keys /tmp/disk.key {{ quote(absolute_path(ROOT / PROVISIONING_DIR / HOST + ".key")) }} \
        --build-on remote \
        {{ ARGS }}

[group('deploy')]
[unix]
[working-directory("../..")]
[doc('Remotely update the ganymede configuration.')]
update *ARGS:
    nix run github:serokell/deploy-rs -- {{ quote(".#" + HOST) }} --remote-build {{ ARGS }}
